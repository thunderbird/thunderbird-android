buildscript {
    ext {
        buildConfig = [
                'compileSdk': 29,
                'targetSdk': 28,
                'minSdk': 21,
                'buildTools': '29.0.2',
                'robolectricSdk': 28
        ]

        versions = [
                'kotlin': '1.3.50',
                'androidxAppCompat': '1.0.2',
                'androidxRecyclerView': '1.0.0',
                'androidxLifecycleExtensions': '2.0.0',
                'androidxAnnotation': '1.0.1',
                'androidxNavigation': '2.0.0',
                'androidxConstraintLayout': '1.1.3',
                'androidxWorkManager': '2.2.0',
                'materialComponents': '1.0.0',
                'coreKtx': '1.0.1',
                'preferencesFix': '1.0.0',
                'okio': '1.14.0',
                'timber': '4.5.1',
                'koin': '0.9.1',
                'commonsIo': '2.4',
                'mime4j': '0.8.1',

                // Note: Only use 3.12.x versions until we raise minSdkVersion to 21
                'okhttp': '3.12.1',

                'androidxTestRunner': '1.1.1',
                'junit': '4.12',
                'robolectric': '4.2.1',
                'mockito': '2.18.0',
                'mockitoKotlin': '1.5.0',
                'truth': '0.35'
        ]

        javaVersion = JavaVersion.VERSION_1_8
        kotlinJvmVersion = "1.8"
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"
    }
}

project.ext {
    testCoverage = project.hasProperty('testCoverage')
}

subprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    configurations.all {
        resolutionStrategy {
            force "androidx.annotation:annotation:${versions.androidxAnnotation}"

            // org.koin:koin-android-architecture depends on android.arch.lifecycle:extensions
            force "androidx.lifecycle:lifecycle-extensions:${versions.androidxLifecycleExtensions}"

            // com.github.amlcurran.showcaseview:library depends on com.android.support:support-v4
            force "androidx.legacy:legacy-support-v4:1.0.0"
        }
    }
}
